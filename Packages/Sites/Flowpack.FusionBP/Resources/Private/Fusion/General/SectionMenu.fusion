prototype(Flowpack.FusionBP:General.SectionMenu) < prototype(Neos.Fusion:Collection) {
    @process.tmpl = ${'<ul class="SectionMenu">' + value + '</ul>'}
    collection = ${q(site).children('[instanceof Flowpack.FusionBP:Document.Section]').get()}
    itemName = 'section'
    itemRenderer = Neos.Fusion:Tag {
        tagName = 'li'
        attributes.class = 'SectionMenu-item'
        attributes.class.@process.menuClass = ${value + (node == documentNode && ' isCurrent')}
        content = Neos.Fusion:Array {
            1 = Neos.Fusion:Tag {
                tagName = 'a'
                attributes.href = NodeUri {
                    node = ${section}
                }
                content = ${section.properties.title}
            }
            2 = Flowpack.FusionBP:General.HeadlineMenu
        }
    }
}

prototype(Flowpack.FusionBP:General.HeadlineMenu) < prototype(Neos.Fusion:Collection) {
    @process.tmpl = ${'<ul class="SectionMenu">' + value + '</ul>'}
    collection = ${q(section).find('[instanceof Flowpack.FusionBP:Content.Headline]').get()}
    itemName = 'headline'
    itemRenderer = Neos.Fusion:Tag {
        @process.tmpl = ${'<li>' + value + '</li>'}
        tagName = 'a'
        attributes.href = NodeUri {
            node = ${section}
            @process.anchor = ${value + '#' + headline.name}
        }
        content = ${headline.properties.text}
    }
}
